

	OPTIONS COMPRESS = YES;
	
/*	00.逻辑库 */
	LIBNAME ANA "C:\Users\IvanXu\Desktop\data\hy_round1";
	%LET CP = C:\Users\IvanXu\Desktop\code\Hy_round1;
	%LET DP = C:\Users\IvanXu\Desktop\data\hy_round1\hy_round1_train_20200102\hy_round1_train_20200102;
	%LET TP = C:\Users\IvanXu\Desktop\data\hy_round1\hy_round1_testA_20200102\hy_round1_testA_20200102;

	%LET PN = 000004;



	
/*	01.数据读取 */
	PROC PRINTTO LOG = "&CP.\RUN.log" NEW;RUN;
	
		%MACRO R();
			PROC DELETE DATA = ANA.RESULT;RUN;
			DATA ANA.RESULT;
			FORMAT VAR1 VAR2 VAR3 VAR4 VAR5 VAR6 VAR7 $30.;
			STOP;
			RUN;

			%DO I = 0 %TO 6999;
				%PUT &I.;
				
				FILENAME INPT "&DP.\&I..csv" ENCODING = "UTF-8";
				PROC IMPORT 
					DATAFILE = INPT
					OUT = DEMO 
					DBMS = CSV 
					REPLACE;
					GETNAMES = NO;
				RUN;

				PROC APPEND DATA = DEMO BASE = ANA.RESULT;RUN;
				PROC DELETE DATA = DEMO;RUN;
			%END;
			
			DATA ANA.RESULT;
			SET ANA.RESULT;
			WHERE VAR7 ^= "type";
			RUN;
		%MEND;
		

		%MACRO T();
			PROC DELETE DATA = ANA.TEST;RUN;

			%DO I = 7000 %TO 8999;
				%PUT &I.;

				FILENAME INPT "&TP.\&I..csv" ENCODING = "UTF-8";
				PROC IMPORT
					DATAFILE = INPT
					OUT = DEMO
					DBMS = CSV
					REPLACE;
					GETNAMES = NO;
				RUN;

				DATA DEMO;
				SET DEMO(WHERE = (VAR6 ^= "time"));
				FORMAT
					ID $4.
					X_1 X_2 X_1V X_2V 24.12
					X_3 X_3_1 8.2 X_3_2 8.4
					X_4 X_4_1 8.2 X_4_2 8.4 X_3_4 8.2
					X_5_MON X_5_DAY X_5_HOR X_5_MIS X_5_SEC 8. X_5_DMY DATETIME20.
/*					X_5_DMYW 2. */
				;
				ID = VAR1;
				X_1 = VAR2 + 0;
				X_2 = VAR3 + 0;
				X_1V = X_1/20037508.34*180;
				X_2V = 180/CONSTANT("PI")*(2*ATAN(EXP(X_2/20037508.34*180*CONSTANT("PI")/180))-CONSTANT("PI")/2);
				X_3 = VAR4 + 0;
				X_3_1 = X_3 > 0;
				X_3_2 = X_3/100;
				X_4 = VAR5 + 0;
				X_4_1 = X_4 > 0;
				X_4_2 = X_4/360;
				X_3_4 = X_3 > 0 OR X_4 > 0;
				X_5_MON = SUBSTR(COMPRESS(VAR6,"0123456789","K"),1,2)+0;
				X_5_DAY = SUBSTR(COMPRESS(VAR6,"0123456789","K"),3,2)+0;
				X_5_HOR = SUBSTR(COMPRESS(VAR6,"0123456789","K"),5,2)+0;
				X_5_MIS = SUBSTR(COMPRESS(VAR6,"0123456789","K"),7,2)+0;
				X_5_SEC = SUBSTR(COMPRESS(VAR6,"0123456789","K"),9,2)+0;
				X_5_DMY = DHMS(MDY(X_5_MON,X_5_DAY,2019),X_5_HOR,X_5_MIS,X_5_SEC);
/*				X_5_DMYW = WEEKDAY(DATEPART(X_5_DMY));*/
				KEEP ID X_:;
				RUN;

				PROC APPEND DATA = DEMO BASE = ANA.TEST;RUN;
				PROC DELETE DATA = DEMO;RUN;
			%END;
		%MEND;

/*		%R();*/
		%T();

	PROC PRINTTO LOG = LOG;RUN;
	

	DATA ANA.TEST;
	SET ANA.TEST;
	FORMAT 
		X_1V X_2V 24.12
		X_3_1 8.2 X_3_2 8.4
		X_4_1 8.2 X_4_2 8.4 X_3_4 8.2
/*		X_5_DMYW 2. */
	;
	X_1V = X_1/20037508.34*180;
	X_2V = 180/CONSTANT("PI")*(2*ATAN(EXP(X_2/20037508.34*180*CONSTANT("PI")/180))-CONSTANT("PI")/2);
	X_3_1 = X_3 > 0;
	X_3_2 = X_3/100;
	X_4_1 = X_4 > 0;
	X_4_2 = X_4/360;
	X_3_4 = X_3 > 0 OR X_4 > 0;
/*	X_5_DMYW = WEEKDAY(DATEPART(X_5_DMY));*/
	RUN;
	
/*	02.训练数据集 */
	PROC FREQ DATA = ANA.RESULT;
	TABLE VAR7/OUT = DEMO;
	RUN;

	DATA ANA.DATA1;
	SET ANA.RESULT;
	FORMAT 
		ID $4.
		TARGET 1. X_1 X_2 X_1V X_2V 24.12 
		X_3 X_3_1 8.2 X_3_2 8.4
		X_4 X_4_1 8.2 X_4_2 8.4 X_3_4 8.2 
		X_5_MON X_5_DAY X_5_HOR X_5_MIS X_5_SEC 8. X_5_DMY DATETIME20.
/*		X_5_DMYW 2. */
	;
	IF VAR7 = "刺网" THEN TARGET = 1;
	IF VAR7 = "围网" THEN TARGET = 0;
	IF VAR7 = "拖网" THEN TARGET = 0;
	ID = VAR1;
	X_1 = VAR2 + 0;
	X_2 = VAR3 + 0;
	X_1V = X_1/20037508.34*180;
	X_2V = 180/CONSTANT("PI")*(2*ATAN(EXP(X_2/20037508.34*180*CONSTANT("PI")/180))-CONSTANT("PI")/2);
	X_3 = VAR4 + 0;
	X_3_1 = X_3 > 0;
	X_3_2 = X_3/100;
	X_4 = VAR5 + 0;
	X_4_1 = X_4 > 0;
	X_4_2 = X_4/360;
	X_3_4 = X_3 > 0 OR X_4 > 0;
	X_5_MON = SUBSTR(COMPRESS(VAR6,"0123456789","K"),1,2)+0;
	X_5_DAY = SUBSTR(COMPRESS(VAR6,"0123456789","K"),3,2)+0;
	X_5_HOR = SUBSTR(COMPRESS(VAR6,"0123456789","K"),5,2)+0;
	X_5_MIS = SUBSTR(COMPRESS(VAR6,"0123456789","K"),7,2)+0;
	X_5_SEC = SUBSTR(COMPRESS(VAR6,"0123456789","K"),9,2)+0;
	X_5_DMY = DHMS(MDY(X_5_MON,X_5_DAY,2019),X_5_HOR,X_5_MIS,X_5_SEC);
/*	X_5_DMYW = WEEKDAY(DATEPART(X_5_DMY));*/
	KEEP ID X_: TARGET;
	RUN;
	PROC PRINT DATA = ANA.DATA1(OBS=10);
	RUN;

	DATA ANA.DATA2;
	SET ANA.RESULT;
	FORMAT 
		ID $4.
		TARGET 1. X_1 X_2 X_1V X_2V 24.12 
		X_3 X_3_1 8.2 X_3_2 8.4
		X_4 X_4_1 8.2 X_4_2 8.4 X_3_4 8.2 
		X_5_MON X_5_DAY X_5_HOR X_5_MIS X_5_SEC 8. X_5_DMY DATETIME20.
/*		X_5_DMYW 2. */
	;
	IF VAR7 = "刺网" THEN TARGET = 0;
	IF VAR7 = "围网" THEN TARGET = 1;
	IF VAR7 = "拖网" THEN TARGET = 0;
	ID = VAR1;
	X_1 = VAR2 + 0;
	X_2 = VAR3 + 0;
	X_1V = X_1/20037508.34*180;
	X_2V = 180/CONSTANT("PI")*(2*ATAN(EXP(X_2/20037508.34*180*CONSTANT("PI")/180))-CONSTANT("PI")/2);
	X_3 = VAR4 + 0;
	X_3_1 = X_3 > 0;
	X_3_2 = X_3/100;
	X_4 = VAR5 + 0;
	X_4_1 = X_4 > 0;
	X_4_2 = X_4/360;
	X_3_4 = X_3 > 0 OR X_4 > 0;
	X_5_MON = SUBSTR(COMPRESS(VAR6,"0123456789","K"),1,2)+0;
	X_5_DAY = SUBSTR(COMPRESS(VAR6,"0123456789","K"),3,2)+0;
	X_5_HOR = SUBSTR(COMPRESS(VAR6,"0123456789","K"),5,2)+0;
	X_5_MIS = SUBSTR(COMPRESS(VAR6,"0123456789","K"),7,2)+0;
	X_5_SEC = SUBSTR(COMPRESS(VAR6,"0123456789","K"),9,2)+0;
	X_5_DMY = DHMS(MDY(X_5_MON,X_5_DAY,2019),X_5_HOR,X_5_MIS,X_5_SEC);
/*	X_5_DMYW = WEEKDAY(DATEPART(X_5_DMY));*/
	KEEP ID X_: TARGET;
	RUN;

	DATA ANA.DATA3;
	SET ANA.RESULT;
	FORMAT 
		ID $4.
		TARGET 1. X_1 X_2 X_1V X_2V 24.12 
		X_3 X_3_1 8.2 X_3_2 8.4
		X_4 X_4_1 8.2 X_4_2 8.4 X_3_4 8.2 
		X_5_MON X_5_DAY X_5_HOR X_5_MIS X_5_SEC 8. X_5_DMY DATETIME20.
/*		X_5_DMYW 2. */
	;
	IF VAR7 = "刺网" THEN TARGET = 0;
	IF VAR7 = "围网" THEN TARGET = 0;
	IF VAR7 = "拖网" THEN TARGET = 1;
	ID = VAR1;
	X_1 = VAR2 + 0;
	X_2 = VAR3 + 0;
	X_1V = X_1/20037508.34*180;
	X_2V = 180/CONSTANT("PI")*(2*ATAN(EXP(X_2/20037508.34*180*CONSTANT("PI")/180))-CONSTANT("PI")/2);
	X_3 = VAR4 + 0;
	X_3_1 = X_3 > 0;
	X_3_2 = X_3/100;
	X_4 = VAR5 + 0;
	X_4_1 = X_4 > 0;
	X_4_2 = X_4/360;
	X_3_4 = X_3 > 0 OR X_4 > 0;
	X_5_MON = SUBSTR(COMPRESS(VAR6,"0123456789","K"),1,2)+0;
	X_5_DAY = SUBSTR(COMPRESS(VAR6,"0123456789","K"),3,2)+0;
	X_5_HOR = SUBSTR(COMPRESS(VAR6,"0123456789","K"),5,2)+0;
	X_5_MIS = SUBSTR(COMPRESS(VAR6,"0123456789","K"),7,2)+0;
	X_5_SEC = SUBSTR(COMPRESS(VAR6,"0123456789","K"),9,2)+0;
	X_5_DMY = DHMS(MDY(X_5_MON,X_5_DAY,2019),X_5_HOR,X_5_MIS,X_5_SEC);
/*	X_5_DMYW = WEEKDAY(DATEPART(X_5_DMY));*/
	KEEP ID X_: TARGET;
	RUN;




/*	03.导入模型宏 */
	%INCLUDE "&CP.\02.MacroS1.sas";
	%INCLUDE "&CP.\02.MacroS2.sas";
	%INCLUDE "&CP.\02.MacroS3.sas";


/*	04.F1分数计算 */
	DATA DATA1;
	SET ANA.DATA1;
	%S1();
	KEEP P_TARGET1 TARGET;
	RUN;

	DATA DATA2;
	SET ANA.DATA2;
	%S2();
	KEEP P_TARGET1 TARGET;
	RUN;

	DATA DATA3;
	SET ANA.DATA3;
	%S3();
	KEEP P_TARGET1 TARGET;
	RUN;
	
	%MACRO F1(DT=,);
		PROC DELETE DATA = &DT._F1;
		RUN;

		%DO I = 1 %TO 100;
			%LET T = %SYSEVALF(&I./100);
			%PUT &T.;

			PROC SQL;
			CREATE TABLE _T1 AS 
			SELECT
				&T. AS T,
				SUM(CASE WHEN P_TARGET1 > &T. AND TARGET = 1 THEN 1 ELSE 0 END)/
					SUM(CASE WHEN P_TARGET1 > &T. THEN 1 ELSE 0 END) AS P,
				SUM(CASE WHEN P_TARGET1 > &T. AND TARGET = 1 THEN 1 ELSE 0 END)/
					SUM(CASE WHEN TARGET = 1 THEN 1 ELSE 0 END) AS R,
				2 * (CALCULATED P) * (CALCULATED R)/((CALCULATED P) + (CALCULATED R)) AS F1
			FROM &DT.;
			QUIT;

			PROC APPEND DATA = _T1 BASE = &DT._F1;RUN;
			PROC DELETE DATA = _T1;RUN;
		%END;
		
		PROC SQL;
		CREATE TABLE &DT._F1 AS 
		SELECT
			*,
			MAX(F1) AS MAXF1,
			CASE WHEN F1 = CALCULATED MAXF1 THEN 1 ELSE 0 END AS IS_MAXF1
		FROM &DT._F1;
		QUIT;

		PROC UNIVARIATE DATA = &DT._F1;
		VAR F1;
		RUN;

	%MEND;
	%F1(DT=DATA1);
	%F1(DT=DATA2);
	%F1(DT=DATA3);
	
	PROC SQL NOPRINT;
		SELECT 
			MAX(CASE WHEN IS_MAXF1 = 1 THEN T ELSE . END) INTO: TDATA1 
		FROM DATA1_F1;
		SELECT 
			MAX(CASE WHEN IS_MAXF1 = 1 THEN T ELSE . END) INTO: TDATA2 
		FROM DATA2_F1;
		SELECT 
			MAX(CASE WHEN IS_MAXF1 = 1 THEN T ELSE . END) INTO: TDATA3
		FROM DATA3_F1;
		
		CREATE TABLE ANA.R&PN. AS 
		SELECT * 
		FROM (
			SELECT * FROM DATA1_F1 UNION ALL 
			SELECT * FROM DATA2_F1 UNION ALL
			SELECT * FROM DATA3_F1
		)
		WHERE IS_MAXF1 = 1;
	QUIT;
	%PUT &TDATA1. &TDATA2. &TDATA3.;

	PROC PRINT DATA = ANA.R&PN.;
	RUN;
	

/*	05.模型结果 */
	DATA TEST1;
	SET ANA.TEST;
	%S1();
	FORMAT MTYPE 1. FID $32.;
	MTYPE = 1;
	FID = PUT(MD5(CAT(ID, CATX("|", OF X_:))), HEX32.);
	RUN;
	PROC SORT DATA = TEST1 NODUPKEY;
	BY FID;
	RUN;

	DATA TEST2;
	SET ANA.TEST;
	%S2();
	FORMAT MTYPE 1. FID $32.;
	MTYPE = 1;
	FID = PUT(MD5(CAT(ID, CATX("|", OF X_:))), HEX32.);
	RUN;
	PROC SORT DATA = TEST2 NODUPKEY;
	BY FID;
	RUN;

	DATA TEST3;
	SET ANA.TEST;
	%S3();
	FORMAT MTYPE 1. FID $32.;
	MTYPE = 1;
	FID = PUT(MD5(CAT(ID, CATX("|", OF X_:))), HEX32.);
	RUN;
	PROC SORT DATA = TEST3 NODUPKEY;
	BY FID;
	RUN;

	DATA TEST123;
	MERGE 
		TEST1(IN=T1 RENAME = (P_TARGET1 = P1) KEEP = FID ID P_TARGET1)
		TEST2(IN=T2 RENAME = (P_TARGET1 = P2) KEEP = FID P_TARGET1)
		TEST3(IN=T3 RENAME = (P_TARGET1 = P3) KEEP = FID P_TARGET1)
	;
	BY FID;
	IF T1 OR T2 OR T3;
	TP1 = P1 > &TDATA1.;
	TP2 = P2 > &TDATA2.;
	TP3 = P3 > &TDATA3.;
	RUN;
	
	PROC SQL;
	CREATE TABLE RESULT AS 
	SELECT
		ID,
		SUM(1) AS CNT,
		MEAN(TP1) AS TP1,
		MEAN(TP2) AS TP2,
		MEAN(TP3) AS TP3
	FROM TEST123
	GROUP BY ID;
	QUIT;

	DATA RESULT;
	SET RESULT;
	FORMAT T $6.;
	/* 1 刺网 2 围网 3 拖网 */
	IF TP1 = MAX(TP1, TP2, TP3)
		THEN T = "刺网";
	ELSE IF TP2 = MAX(TP1, TP2, TP3)
		THEN T = "围网";
	ELSE IF TP3 = MAX(TP1, TP2, TP3)
		THEN T = "拖网";
	ELSE T = "拖网";
	RUN;
	
	FILENAME EXPT "C:\Users\IvanXu\Desktop\data\hy_round1\result&PN..csv" ENCODING="UTF-8";
	PROC EXPORT 
	    DATA = RESULT(KEEP = ID T)
	    OUTFILE = EXPT DBMS = CSV REPLACE;
	    PUTNAMES = NO;
	RUN;
	
	

/*
									M1	 M2	  M3
	训练: Kolmogorov-Smirnov 统计量 0.55 0.65 0.53
	验证: Kolmogorov-Smirnov 统计量 0.55 0.65 0.53
	测试: Kolmogorov-Smirnov 统计量 0.54 0.65 0.52
*/



